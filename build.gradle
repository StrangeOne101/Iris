plugins {
    id 'java'
    id 'io.freefair.lombok' version '5.2.1'
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'maven-publish'
}

group 'com.volmit.iris'
version '1.4.10-LEGACY'
def apiVersion = '1.14'
def name = 'Iris'
def main = 'com.volmit.iris.Iris'

// ==============================================================
registerCustomOutputTask('Cyberpwn', 'C://Users/cyberpwn/Documents/development/server/plugins');
registerCustomOutputTask('ArcaneArts', 'C://Users/cyberpwn/Documents/development/server/plugins');
registerCustomOutputTask('Strange', 'D://Servers/HoloParadise Bungee Server/Survival/plugins');
// ==============================================================

configurations {
    provided
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

processResources {
    filesMatching('**/plugin.yml') {
        expand(
            'name': name.toString(),
            'version': version.toString(),
            'main': main.toString(),
            'apiversion': apiVersion.toString()
        )
    }
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    maven {
        allowInsecureProtocol true
        url "http://archive.arcane.art/repository/arcane/"
    }
}

shadowJar
{
    minimize()
    dependencies {
        include(dependency('org.zeroturnaround:zt-zip:1.14'))
        include(dependency('io.papermc:paperlib:1.0.5'))
        include(dependency('com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2'))
    }
}

def registerCustomOutputTask(name, path) {
    tasks.register('build' + name, Copy) {
        group('development')
        outputs.upToDateWhen{false}
        dependsOn ':shadowJar'
        from(file('build/libs/Iris-' + version + '-all.jar'))
        into(file(path))
        rename { String fileName ->
            fileName.replace('Iris-' + version + '-all.jar', "Iris.jar")
        }
    }
}


dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    implementation 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2'
    implementation 'org.zeroturnaround:zt-zip:1.14'
    implementation 'io.papermc:paperlib:1.0.5'
    implementation 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    implementation 'org.bukkit.craftbukkit:1.16.3:1.16.3'
    implementation 'org.bukkit.craftbukkit:1.16.4:1.16.4'
    implementation 'com.bergerkiller.bukkit:BKCommonLib:1.16.4-v2'
    implementation 'com.sk89q.worldedit:worldedit-bukkit:7.2.0-SNAPSHOT'
    implementation 'io.lumine.xikage:MythicMobs:4.9.1'
    implementation 'com.google.code.gson:gson:2.8.5'
}